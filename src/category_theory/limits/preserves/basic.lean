/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Reid Barton, Bhavik Mehta
-/
import category_theory.limits.limits

/-!
# Preservation and reflection of (co)limits.

There are various distinct notions of "preserving limits". The one we
aim to capture here is: A functor F : C ‚Üí D "preserves limits" if it
sends every limit cone in C to a limit cone in D. Informally, F
preserves all the limits which exist in C.

Note that:

* Of course, we do not want to require F to *strictly* take chosen
  limit cones of C to chosen limit cones of D. Indeed, the above
  definition makes no reference to a choice of limit cones so it makes
  sense without any conditions on C or D.

* Some diagrams in C may have no limit. In this case, there is no
  condition on the behavior of F on such diagrams. There are other
  notions (such as "flat functor") which impose conditions also on
  diagrams in C with no limits, but these are not considered here.

In order to be able to express the property of preserving limits of a
certain form, we say that a functor F preserves the limit of a
diagram K if F sends every limit cone on K to a limit cone. This is
vacuously satisfied when K does not admit a limit, which is consistent
with the above definition of "preserves limits".
-/

open category_theory

noncomputable theory

namespace category_theory.limits

universes v u‚ÇÅ u‚ÇÇ u‚ÇÉ -- declare the `v`'s first; see `category_theory.category` for an explanation

variables {C : Type u‚ÇÅ} [category.{v} C]
variables {D : Type u‚ÇÇ} [category.{v} D]

variables {J : Type v} [small_category J] {K : J ‚•§ C}

/--
A functor `F` preserves limits of `K` (written as `preserves_limit K F`)
if `F` maps any limit cone over `K` to a limit cone.
-/
class preserves_limit (K : J ‚•§ C) (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(preserves : Œ† {c : cone K}, is_limit c ‚Üí is_limit (F.map_cone c))
/--
A functor `F` preserves colimits of `K` (written as `preserves_colimit K F`)
if `F` maps any colimit cocone over `K` to a colimit cocone.
-/
class preserves_colimit (K : J ‚•§ C) (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(preserves : Œ† {c : cocone K}, is_colimit c ‚Üí is_colimit (F.map_cocone c))

/-- We say that `F` preserves limits of shape `J` if `F` preserves limits for every diagram
    `K : J ‚•§ C`, i.e., `F` maps limit cones over `K` to limit cones. -/
class preserves_limits_of_shape (J : Type v) [small_category J] (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(preserves_limit : Œ† {K : J ‚•§ C}, preserves_limit K F)
/-- We say that `F` preserves colimits of shape `J` if `F` preserves colimits for every diagram
    `K : J ‚•§ C`, i.e., `F` maps colimit cocones over `K` to colimit cocones. -/
class preserves_colimits_of_shape (J : Type v) [small_category J] (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(preserves_colimit : Œ† {K : J ‚•§ C}, preserves_colimit K F)

/-- We say that `F` preserves limits if it sends limit cones over any diagram to limit cones. -/
class preserves_limits (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ (v+1)) :=
(preserves_limits_of_shape : Œ† {J : Type v} [ùí• : small_category J], by exactI preserves_limits_of_shape J F)
/-- We say that `F` preserves colimits if it sends colimit cocones over any diagram to colimit
    cocones.-/
class preserves_colimits (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ (v+1)) :=
(preserves_colimits_of_shape : Œ† {J : Type v} [ùí• : small_category J], by exactI preserves_colimits_of_shape J F)

attribute [instance, priority 100] -- see Note [lower instance priority]
  preserves_limits_of_shape.preserves_limit preserves_limits.preserves_limits_of_shape
  preserves_colimits_of_shape.preserves_colimit preserves_colimits.preserves_colimits_of_shape

instance preserves_limit_subsingleton (K : J ‚•§ C) (F : C ‚•§ D) : subsingleton (preserves_limit K F) :=
by split; rintros ‚ü®a‚ü© ‚ü®b‚ü©; congr
instance preserves_colimit_subsingleton (K : J ‚•§ C) (F : C ‚•§ D) : subsingleton (preserves_colimit K F) :=
by split; rintros ‚ü®a‚ü© ‚ü®b‚ü©; congr

instance preserves_limits_of_shape_subsingleton (J : Type v) [small_category J] (F : C ‚•§ D) :
  subsingleton (preserves_limits_of_shape J F) :=
by { split, intros, cases a, cases b, congr }
instance preserves_colimits_of_shape_subsingleton (J : Type v) [small_category J] (F : C ‚•§ D) :
  subsingleton (preserves_colimits_of_shape J F) :=
by { split, intros, cases a, cases b, congr }

instance preserves_limits_subsingleton (F : C ‚•§ D) : subsingleton (preserves_limits F) :=
by { split, intros, cases a, cases b, cc }
instance preserves_colimits_subsingleton (F : C ‚•§ D) : subsingleton (preserves_colimits F) :=
by { split, intros, cases a, cases b, cc }

instance id_preserves_limits : preserves_limits (ùü≠ C) :=
{ preserves_limits_of_shape := Œª J ùí•,
  { preserves_limit := Œª K, by exactI ‚ü®Œª c h,
  ‚ü®Œª s, h.lift ‚ü®s.X, Œª j, s.œÄ.app j, Œª j j' f, s.œÄ.naturality f‚ü©,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s j; cases s; exact h.fac _ j,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s m w; rcases s with ‚ü®_, _, _‚ü©; exact h.uniq _ m w‚ü©‚ü© } }

instance id_preserves_colimits : preserves_colimits (ùü≠ C) :=
{ preserves_colimits_of_shape := Œª J ùí•,
  { preserves_colimit := Œª K, by exactI ‚ü®Œª c h,
  ‚ü®Œª s, h.desc ‚ü®s.X, Œª j, s.Œπ.app j, Œª j j' f, s.Œπ.naturality f‚ü©,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s j; cases s; exact h.fac _ j,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s m w; rcases s with ‚ü®_, _, _‚ü©; exact h.uniq _ m w‚ü©‚ü© } }

section
variables {E : Type u‚ÇÉ} [‚Ñ∞ : category.{v} E]
variables (F : C ‚•§ D) (G : D ‚•§ E)

local attribute [elab_simple] preserves_limit.preserves preserves_colimit.preserves

instance comp_preserves_limit [preserves_limit K F] [preserves_limit (K ‚ãô F) G] :
  preserves_limit K (F ‚ãô G) :=
‚ü®Œª c h, preserves_limit.preserves (preserves_limit.preserves h)‚ü©

instance comp_preserves_colimit [preserves_colimit K F] [preserves_colimit (K ‚ãô F) G] :
  preserves_colimit K (F ‚ãô G) :=
‚ü®Œª c h, preserves_colimit.preserves (preserves_colimit.preserves h)‚ü©

end

/-- If F preserves one limit cone for the diagram K,
  then it preserves any limit cone for K. -/
def preserves_limit_of_preserves_limit_cone {F : C ‚•§ D} {t : cone K}
  (h : is_limit t) (hF : is_limit (F.map_cone t)) : preserves_limit K F :=
‚ü®Œª t' h', is_limit.of_iso_limit hF (functor.map_iso _ (is_limit.unique_up_to_iso h h'))‚ü©

/-- Transfer preservation of limits along a natural isomorphism in the shape. -/
def preserves_limit_of_iso {K‚ÇÅ K‚ÇÇ : J ‚•§ C} (F : C ‚•§ D) (h : K‚ÇÅ ‚âÖ K‚ÇÇ) [preserves_limit K‚ÇÅ F] :
  preserves_limit K‚ÇÇ F :=
{ preserves := Œª c t,
  begin
    have t' := is_limit.of_right_adjoint (cones.postcompose_equivalence h).inverse t,
    let hF := iso_whisker_right h F,
    have := is_limit.of_right_adjoint (cones.postcompose_equivalence hF).functor
              (preserves_limit.preserves t'),
    apply is_limit.of_iso_limit this,
    refine cones.ext (iso.refl _) (Œª j, _),
    dsimp,
    rw [‚Üê F.map_comp],
    simp,
  end }

/-- If F preserves one colimit cocone for the diagram K,
  then it preserves any colimit cocone for K. -/
def preserves_colimit_of_preserves_colimit_cocone {F : C ‚•§ D} {t : cocone K}
  (h : is_colimit t) (hF : is_colimit (F.map_cocone t)) : preserves_colimit K F :=
‚ü®Œª t' h', is_colimit.of_iso_colimit hF (functor.map_iso _ (is_colimit.unique_up_to_iso h h'))‚ü©

/-- Transfer preservation of colimits along a natural isomorphism in the shape. -/
def preserves_colimit_of_iso {K‚ÇÅ K‚ÇÇ : J ‚•§ C} (F : C ‚•§ D) (h : K‚ÇÅ ‚âÖ K‚ÇÇ) [preserves_colimit K‚ÇÅ F] :
  preserves_colimit K‚ÇÇ F :=
{ preserves := Œª c t,
  begin
    have t' := is_colimit.of_left_adjoint (cocones.precompose_equivalence h).functor t,
    let hF := iso_whisker_right h F,
    have := is_colimit.of_left_adjoint (cocones.precompose_equivalence hF).inverse
              (preserves_colimit.preserves t'),
    apply is_colimit.of_iso_colimit this,
    refine cocones.ext (iso.refl _) (Œª j, _),
    dsimp,
    rw [‚Üê F.map_comp],
    simp,
  end }

/--
A convenience function for `preserves_limit`, which takes the functor as an explicit argument to
guide typeclass resolution.
-/
def is_limit_of_preserves (F : C ‚•§ D) {c : cone K} (t : is_limit c) [preserves_limit K F] :
  is_limit (F.map_cone c) :=
preserves_limit.preserves t

/--
A convenience function for `preserves_colimit`, which takes the functor as an explicit argument to
guide typeclass resolution.
-/
def is_colimit_of_preserves (F : C ‚•§ D) {c : cocone K} (t : is_colimit c)
  [preserves_colimit K F] :
  is_colimit (F.map_cocone c) :=
preserves_colimit.preserves t

/--
A functor `F : C ‚•§ D` reflects limits for `K : J ‚•§ C` if
whenever the image of a cone over `K` under `F` is a limit cone in `D`,
the cone was already a limit cone in `C`.
Note that we do not assume a priori that `D` actually has any limits.
-/
class reflects_limit (K : J ‚•§ C) (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(reflects : Œ† {c : cone K}, is_limit (F.map_cone c) ‚Üí is_limit c)
/--
A functor `F : C ‚•§ D` reflects colimits for `K : J ‚•§ C` if
whenever the image of a cocone over `K` under `F` is a colimit cocone in `D`,
the cocone was already a colimit cocone in `C`.
Note that we do not assume a priori that `D` actually has any colimits.
-/
class reflects_colimit (K : J ‚•§ C) (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(reflects : Œ† {c : cocone K}, is_colimit (F.map_cocone c) ‚Üí is_colimit c)

/--
A functor `F : C ‚•§ D` reflects limits of shape `J` if
whenever the image of a cone over some `K : J ‚•§ C` under `F` is a limit cone in `D`,
the cone was already a limit cone in `C`.
Note that we do not assume a priori that `D` actually has any limits.
-/
class reflects_limits_of_shape (J : Type v) [small_category J] (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(reflects_limit : Œ† {K : J ‚•§ C}, reflects_limit K F)
/--
A functor `F : C ‚•§ D` reflects colimits of shape `J` if
whenever the image of a cocone over some `K : J ‚•§ C` under `F` is a colimit cocone in `D`,
the cocone was already a colimit cocone in `C`.
Note that we do not assume a priori that `D` actually has any colimits.
-/
class reflects_colimits_of_shape (J : Type v) [small_category J] (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ v) :=
(reflects_colimit : Œ† {K : J ‚•§ C}, reflects_colimit K F)

/--
A functor `F : C ‚•§ D` reflects limits if
whenever the image of a cone over some `K : J ‚•§ C` under `F` is a limit cone in `D`,
the cone was already a limit cone in `C`.
Note that we do not assume a priori that `D` actually has any limits.
-/
class reflects_limits (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ (v+1)) :=
(reflects_limits_of_shape : Œ† {J : Type v} {ùí• : small_category J}, by exactI reflects_limits_of_shape J F)
/--
A functor `F : C ‚•§ D` reflects colimits if
whenever the image of a cocone over some `K : J ‚•§ C` under `F` is a colimit cocone in `D`,
the cocone was already a colimit cocone in `C`.
Note that we do not assume a priori that `D` actually has any colimits.
-/
class reflects_colimits (F : C ‚•§ D) : Type (max u‚ÇÅ u‚ÇÇ (v+1)) :=
(reflects_colimits_of_shape : Œ† {J : Type v} {ùí• : small_category J}, by exactI reflects_colimits_of_shape J F)

instance reflects_limit_subsingleton (K : J ‚•§ C) (F : C ‚•§ D) : subsingleton (reflects_limit K F) :=
by split; rintros ‚ü®a‚ü© ‚ü®b‚ü©; congr
instance reflects_colimit_subsingleton (K : J ‚•§ C) (F : C ‚•§ D) : subsingleton (reflects_colimit K F) :=
by split; rintros ‚ü®a‚ü© ‚ü®b‚ü©; congr

instance reflects_limits_of_shape_subsingleton (J : Type v) [small_category J] (F : C ‚•§ D) :
  subsingleton (reflects_limits_of_shape J F) :=
by { split, intros, cases a, cases b, congr }
instance reflects_colimits_of_shape_subsingleton (J : Type v) [small_category J] (F : C ‚•§ D) :
  subsingleton (reflects_colimits_of_shape J F) :=
by { split, intros, cases a, cases b, congr }

instance reflects_limits_subsingleton (F : C ‚•§ D) : subsingleton (reflects_limits F) :=
by { split, intros, cases a, cases b, cc }
instance reflects_colimits_subsingleton (F : C ‚•§ D) : subsingleton (reflects_colimits F) :=
by { split, intros, cases a, cases b, cc }

@[priority 100] -- see Note [lower instance priority]
instance reflects_limit_of_reflects_limits_of_shape (K : J ‚•§ C) (F : C ‚•§ D)
  [H : reflects_limits_of_shape J F] : reflects_limit K F :=
reflects_limits_of_shape.reflects_limit
@[priority 100] -- see Note [lower instance priority]
instance reflects_colimit_of_reflects_colimits_of_shape (K : J ‚•§ C) (F : C ‚•§ D)
  [H : reflects_colimits_of_shape J F] : reflects_colimit K F :=
reflects_colimits_of_shape.reflects_colimit

@[priority 100] -- see Note [lower instance priority]
instance reflects_limits_of_shape_of_reflects_limits (F : C ‚•§ D)
  [H : reflects_limits F] : reflects_limits_of_shape J F :=
reflects_limits.reflects_limits_of_shape
@[priority 100] -- see Note [lower instance priority]
instance reflects_colimits_of_shape_of_reflects_colimits (F : C ‚•§ D)
  [H : reflects_colimits F] : reflects_colimits_of_shape J F :=
reflects_colimits.reflects_colimits_of_shape

instance id_reflects_limits : reflects_limits (ùü≠ C) :=
{ reflects_limits_of_shape := Œª J ùí•,
  { reflects_limit := Œª K, by exactI ‚ü®Œª c h,
  ‚ü®Œª s, h.lift ‚ü®s.X, Œª j, s.œÄ.app j, Œª j j' f, s.œÄ.naturality f‚ü©,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s j; cases s; exact h.fac _ j,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s m w; rcases s with ‚ü®_, _, _‚ü©; exact h.uniq _ m w‚ü©‚ü© } }

instance id_reflects_colimits : reflects_colimits (ùü≠ C) :=
{ reflects_colimits_of_shape := Œª J ùí•,
  { reflects_colimit := Œª K, by exactI ‚ü®Œª c h,
  ‚ü®Œª s, h.desc ‚ü®s.X, Œª j, s.Œπ.app j, Œª j j' f, s.Œπ.naturality f‚ü©,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s j; cases s; exact h.fac _ j,
   by cases K; rcases c with ‚ü®_, _, _‚ü©; intros s m w; rcases s with ‚ü®_, _, _‚ü©; exact h.uniq _ m w‚ü©‚ü© } }

section
variables {E : Type u‚ÇÉ} [‚Ñ∞ : category.{v} E]
variables (F : C ‚•§ D) (G : D ‚•§ E)

instance comp_reflects_limit [reflects_limit K F] [reflects_limit (K ‚ãô F) G] :
  reflects_limit K (F ‚ãô G) :=
‚ü®Œª c h, reflects_limit.reflects (reflects_limit.reflects h)‚ü©

instance comp_reflects_colimit [reflects_colimit K F] [reflects_colimit (K ‚ãô F) G] :
  reflects_colimit K (F ‚ãô G) :=
‚ü®Œª c h, reflects_colimit.reflects (reflects_colimit.reflects h)‚ü©

/-- If `F ‚ãô G` preserves limits for `K`, and `G` reflects limits for `K ‚ãô F`,
then `F` preserves limits for `K`. -/
def preserves_limit_of_reflects_of_preserves [preserves_limit K (F ‚ãô G)]
  [reflects_limit (K ‚ãô F) G] : preserves_limit K F :=
‚ü®Œª c h,
 begin
  apply @reflects_limit.reflects _ _ _ _ _ _ _ G,
  change limits.is_limit ((F ‚ãô G).map_cone c),
  exact preserves_limit.preserves h
 end‚ü©

/-- If `F ‚ãô G` preserves colimits for `K`, and `G` reflects colimits for `K ‚ãô F`,
then `F` preserves colimits for `K`. -/
def preserves_colimit_of_reflects_of_preserves [preserves_colimit K (F ‚ãô G)]
  [reflects_colimit (K ‚ãô F) G] : preserves_colimit K F :=
‚ü®Œª c h,
 begin
  apply @reflects_colimit.reflects _ _ _ _ _ _ _ G,
  change limits.is_colimit ((F ‚ãô G).map_cocone c),
  exact preserves_colimit.preserves h
 end‚ü©


end

variable (F : C ‚•§ D)

/-- A fully faithful functor reflects limits. -/
def fully_faithful_reflects_limits [full F] [faithful F] : reflects_limits F :=
{ reflects_limits_of_shape := Œª J ùí•‚ÇÅ, by exactI
  { reflects_limit := Œª K,
    { reflects := Œª c t,
      is_limit.mk_cone_morphism (Œª s, (cones.functoriality K F).preimage (t.lift_cone_morphism _)) $
      begin
        apply (Œª s m, (cones.functoriality K F).map_injective _),
        rw [functor.image_preimage],
        apply t.uniq_cone_morphism,
      end } } }

/-- A fully faithful functor reflects colimits. -/
def fully_faithful_reflects_colimits [full F] [faithful F] : reflects_colimits F :=
{ reflects_colimits_of_shape := Œª J ùí•‚ÇÅ, by exactI
  { reflects_colimit := Œª K,
    { reflects := Œª c t,
      is_colimit.mk_cocone_morphism (Œª s, (cocones.functoriality K F).preimage (t.desc_cocone_morphism _)) $
      begin
        apply (Œª s m, (cocones.functoriality K F).map_injective _),
        rw [functor.image_preimage],
        apply t.uniq_cocone_morphism,
      end } } }

end category_theory.limits
